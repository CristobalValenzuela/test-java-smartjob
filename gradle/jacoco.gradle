apply plugin: 'jacoco'

jacoco {
    toolVersion = "0.8.12"
}

ext['skip'] = ['**/*MapperImpl*', '**/**Configuration*', '**/**Application.class']

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                counter = 'INSTRUCTION'
                value = 'COVEREDRATIO'
                minimum = 0.60
            }
        }
        rule {
            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 0.60
            }
        }
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: skip)
        }))
    }
}

jacocoTestReport {
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: skip)
        }))
    }
    reports {
        xml.required = true
        html.required = true
    }
}

test.finalizedBy jacocoTestCoverageVerification, jacocoTestReport
